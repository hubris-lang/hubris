use ast::*;
use tok::{self, Tok};
use std::str::FromStr;
use unicode_xid::UnicodeXID;

grammar<'input>(text: &'input str)["LALR(1)"];

pub Module: Module =  {
  <lo:@L> "module" <n: Name> <ds: (Item)*> <hi:@R> =>
    Module {
        id: ModuleId(0),
        span: Span::new(lo, hi),
        name: n,
        items: ds,
    },
};

Item: Item = {
  <lo:@L> <d: ItemNoSpan> <hi:@R> => {
    let mut d = d;
    d.set_span(Span::new(lo, hi)); d
  }
};

ItemNoSpan: Item = {
  <d: Inductive> => Item::Inductive(d),
  <f: Def> => Item::Def(f),
  <a: Axiom> => Item::Axiom(a),
  <e: Extern> => Item::Extern(e),
  <i: Import> => Item::Import(i),
  <dc: "DocComment"> => Item::Comment(dc.to_owned()),
};

Inductive: Inductive = {
 "inductive" <n: Name> <ps : Parameters> <ty: (":" Term)?> <attrs: (<Constructor>)*> "end" =>
  Inductive {
    span: Span::dummy(),
    name: n,
    parameters: ps,
    ty: ty.map(|t| t.1).unwrap_or(Term::Type),
    ctors: attrs,
  }
};

Constructor: (Name, Term) = {
    "|" <n: Name> ":" <t: Term> =>  (n, t)
};

Def: Def = {
    "def" <n: Name> <args: Parameters> ":" <t: Term> ":=" <body: Term> "end" =>
      Def {
          span: Span::dummy(),
          name: n,
          args: args,
          ty: t,
          body: body,
      }
};

Axiom: Axiom = {
    "axiom" <n: Name> ":" <t: Term> =>
      Axiom {
        span: Span::dummy(),
        name: n,
        ty: t,
      }
};

Extern: Extern = {
   "extern" <n : Name> ":" <t : Term> =>
    Extern {
      span: Span::dummy(),
      name: n,
      term: t,
    }
};

Import: Name = {
    "import" <n : Name> => n,
};

Parameters: Vec<Binder> = {
  <ps: Binder*> => ps,
};

Binder: Binder = {
    "(" <lo: @L> <ns : NameSeq> <ty : (":" Term)?> <hi: @R> ")" => Binder {
        span: Span::new(lo, hi),
        names: ns,
        ty: ty.map(|ty| ty.1),
        mode: BindingMode::Explicit,
    },
    "{" <lo: @L> <ns : NameSeq> <ty : (":" Term)?> <hi: @R> "}" => Binder {
        span: Span::new(lo, hi),
        names: ns,
        ty: ty.map(|ty| ty.1),
        mode: BindingMode::Implicit,
    },
};

NameSeq: Vec<Name> = {
    <n: Name> <ns: NameSeq> => {
        let mut ns = ns;
        ns.push(n); ns
    },
    <n : Name> => vec![n],
};

pub Term: Term = {
  <lo:@L> <t: TermUS> <hi:@R>  => {
    let mut t = t;
    t.set_span(Span::new(lo, hi));
    t
  }
};

TermUS: Term = {
  <t1: Term1> "->" <t2: Term> => {
  let span = Span::new(
      t1.get_span().lo,
      t2.get_span().hi);

    let mut name =
      Name::from_str("");

    name.set_span(t1.get_span());

    let binder = Binder {
      span: t1.get_span(),
      names: vec![name],
      ty: Some(t1),
      mode: BindingMode::Explicit,
    };

    Term::Forall {
      span: span,
      binders: vec![binder],
      term: Box::new(t2)
    }
},
   "forall" <args: (Binder+)> "," <body: Term> => {
      Term::Forall {
          span: Span::dummy(),
          binders: args,
          term: Box::new(body)
      }
  },
  "fun" <args: (Binder+)> <t: (":" Term)?> "=>" <body: Term> => Term::Lambda {
      span: Span::dummy(),
      args: args,
      ret_ty: Box::new(t.map(|x| x.1)),
      body: Box::new(body)
  },
  "let" <bindings: (Binding ",")+> "in" <body: Term> => {
      Term::Let {
          bindings: bindings.into_iter().map(|b| b.0).collect(),
          body: Box::new(body),
          span: Span::dummy(),
      }
   },
  <t: Term1> => t
};

Binding : (Binder, Term) = {
    <n: Name> ":" <ty: Term> ":=" <rhs: Term> => {
        let binder = Binder {
            span: n.span,
            names: vec![n],
            ty: Some(ty),
            mode: BindingMode::Explicit,
        };

        (binder, rhs)
    }
};

Term1: Term = {
    <f: Term1> <g: Term0> => Term::App {
      span: Span::new(f.get_span().lo, g.get_span().hi),
      fun: Box::new(f),
      arg: Box::new(g)
    },
    <t : Term0> => t,
};

Term0: Term = {
  <lo:@L> <t: Term0US> <hi:@R>  => {
    let mut t = t;
    t.set_span(Span::new(lo, hi));
    t
  }
};

Term0US: Term = {
  <n : Name> => Term::Var { name: n },
  <m : Match> => Term::Match {
    span: Span::dummy(),
    scrutinee: Box::new(m.0),
    cases: m.1
  },
  "Type" => Term::Type,
  "(" <t:TermUS> ")" => t,
};

Match: (Term, Vec<Case>) = {
  "match" <scrutinee: Term> "with" <cs: (Case)*> "end"  =>
  (scrutinee, cs)
};

Case: Case = {
  <lo:@L> "|" <p: Pattern> "=>" <body: Term> <hi:@R> => Case {
      span: Span::new(lo, hi),
      pattern: p,
      rhs: body,
  }
};

Pattern: Pattern = {
    <n : Name> <ns: PatternO*> => Pattern::Constructor(n, ns),
};

PatternO: Pattern = {
    <n : Name> => Pattern::Name(n),
    "(" <p: Pattern> ")" => p,
};

pub Name: Name = {
  <lo:@L> <nk : NameKind> <hi: @R> => {
        Name {
            span: Span::new(lo, hi),
            repr: nk,
        }
    },
};

NameKind: NameKind = {
  <nk: NameVec> => if nk.len() == 1 {
      let unqual = nk[0].clone();
      NameKind::Unqualified(unqual)
  } else {
      NameKind::Qualified(nk)
  },
  "_" => NameKind::Placeholder,
};

NameVec: Vec<String> = {
   <prefix : NameVec> "." <end : "Id"> => {
        let mut prefix = prefix; prefix.push(end.to_string()); prefix
    },
    <s : "Id"> => vec![s.to_string()],
};

extern {
    type Location = usize;
    type Error = tok::Error;
    enum Tok<'input> {
        "def" => Tok::Def,
        "axiom" => Tok::Axiom,
        "end" => Tok::End,
        "extern" => Tok::Extern,
        "forall" => Tok::Forall,
        "fun" => Tok::Fun,
        "inductive" => Tok::Inductive,
        "in" => Tok::In,
        "import" => Tok::Import,
        "let" => Tok::Let,
        "match" => Tok::Match,
        "module" => Tok::Module,
        "pub" => Tok::Pub,
        "Type" => Tok::Type,
        "with" => Tok::With,
        "|" => Tok::Bar,
        ":" => Tok::Colon,
        ":=" => Tok::ColonEq,
        "(" => Tok::LeftParen,
        ")" => Tok::RightParen,
        "{" => Tok::LeftBrace,
        "}" => Tok::RightBrace,
        "," => Tok::Comma,
        "=>" => Tok::FatArrow,
        "->" => Tok::Arrow,
        "_" => Tok::Underscore,
        "." => Tok::Period,
        "DocComment" => Tok::DocComment(<String>),
        "Id" => Tok::Id(<&'input str>),
    }
}
